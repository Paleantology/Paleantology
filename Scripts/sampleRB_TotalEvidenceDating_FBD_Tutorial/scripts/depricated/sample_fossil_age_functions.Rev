function sample_fossil_age_uniform ( TimeTree tree , String calib_file )
{ 
	d = readDataDelimitedFile(file=calib_file, delimiter=TAB, header=true)
	
	for(i in 1:d.size())
	{
		taxonA = d[i][1]
		obs_min = d[i][2]
		obs_max = d[i][3]
		
		t[i] := tmrca(tree, clade(taxonA))
		
		half = (obs_max - obs_min)/2
		obs_med = obs_min + half
		
		min[i] := max(v(t[i] - half,0))
		max[i] := t[i] + half
		
		fossil[i] ~ dnUniform(min[i], max[i])
		fossil[i].clamp(obs_med)
	}
}

function sample_fossil_age_normal ( TimeTree tree , String calib_file )
{ 
	d = readDataDelimitedFile(file=calib_file, delimiter=TAB, header=true)
	
	for(i in 1:d.size())
	{
		taxonA = d[i][1]
		obs_mean = d[i][2]
		stdev = d[i][3]
		
		mean[i] := tmrca(tree, clade(taxonA))
		
		fossil[i] ~ dnNormal(mean[i], stdev)
		
		fossil[i].clamp(obs_mean)
	}
}

function sample_fossil_age_exponential ( TimeTree tree , String calib_file )
{ 
	d = readDataDelimitedFile(file=calib_file, delimiter=TAB, header=true)
	
	for(i in 1:d.size())
	{
		taxonA = d[i][1]
		obs_min = d[i][2]
		lambda = d[i][3]
		
		offset[i] := tmrca(tree, clade(taxonA))
		
		fossil[i] ~ dnExp(lambda, offset[i])
		
		fossil[i].clamp(obs_min)
	}
}